plugins {
    id "maven-publish"
    id "org.jetbrains.kotlin.jvm" version "${kotlinVersion}"
    id "org.jetbrains.kotlin.kapt" version "${kotlinVersion}"
    id "org.jetbrains.kotlin.plugin.allopen" version "${kotlinVersion}"
    id "com.github.ben-manes.versions" version "${gradleVersionCheckVersion}"
    id "io.micronaut.library" version "${micronautGradlePluginVersion}"
}

group = "hu.indicium.dev.lib"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    kapt "io.micronaut:micronaut-inject-java"
    kapt "io.micronaut.data:micronaut-data-processor"
    api "io.micronaut.data:micronaut-data-jdbc"
    api "io.micronaut:micronaut-http-client"
    compileOnly "io.micronaut.kotlin:micronaut-kotlin-runtime"
}


java {
    sourceCompatibility = JavaVersion.toVersion("11")
}

tasks {
    compileKotlin {
        kotlinOptions {
            jvmTarget = "11"
            javaParameters = true
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "11"
            javaParameters = true
        }
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "general"
            from components.java
            artifact sourcesJar
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/svIndicium/$project.name"
            credentials {
                username = System.getenv("GITHUB_USERNAME")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

jar {
    into("META-INF/maven/$project.group/$project.name") {
        from { generatePomFileForMavenJavaPublication }
        rename ".*", "pom.xml"
    }
}
